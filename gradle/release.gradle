apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task versionArchives {
    description = "Creates a continuous-delivery friendly version number for the service"
    def buildNumber = System.getenv('BUILD_NUMBER') ?: 0

    // Default to building SNAPSHOT versions unless the build was executed with the -PreleaseType={type} command line parameter
    if (project.hasProperty('releaseType')) {
        if (project.releaseType == 'release') {
            project.ext.set("releaseVersion", "${project.version}." + buildNumber);
        } else if (project.releaseType == 'snapshot') {
            project.ext.set("releaseVersion", "${project.version}-SNAPSHOT");
        } else {
            throw new RuntimeException("Invalid releaseType specified:  Must be 'release' or 'snapshot'!")
        }
    } else {
        logger.info("Property 'releaseType' was not specified.  Defaulting to 'snapshot'!")
        project.ext.set("releaseVersion", "${project.version}-SNAPSHOT");
    }

    logger.info("Setting artifact version: ${releaseVersion}")
    project.version = "${releaseVersion}"
}
build.dependsOn versionArchives

jar {
    baseName = "${artifact}"
    version = "${releaseVersion}"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'ignitr-oss'
        name = "${artifact}"
        userOrg = 'ignitr'
        desc = 'Library of common components used by Ignition SpringBoot projects'
        websiteUrl = 'https://github.com/ignitr-oss/ignition-springboot-common'
        issueTrackerUrl = 'https://github.com/ignitr-oss/ignition-springboot-common/issues'
        vcsUrl = 'https://github.com/ignitr-oss/ignition-springboot-common.git'
        licenses = ['Apache-2.0']

        version {
            name = "${releaseVersion}"
        }
    }
}
